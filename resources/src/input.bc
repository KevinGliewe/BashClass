//  Import file
># source "helpers.bashc"

// Create global variables
var int SUCCESS_CODE;
var int FAILURE_CODE;

/**
 * Initialize global variables
 */
function void init() {
    var string a;
    SUCCESS_CODE=0;
    FAILURE_CODE=1;
}

/**
 * Source a file
 **/
 function void import(var string file) {
    ># source $1
 }

/**
 * Print a message
 **/
function void print(var any message) {
    ># echo "$1"
}

/**
 * Replace first occurence of a pattern by a given value
 **/
function string replaceFirst(var string pattern, var any value) {
    var string x;
    x = >$ sed "0,/{}/{s/{}/$2/}" <<< "$1" $<;
    return x;
}

/**
 * String format with two arguments
 **/
function string stringf2(var string message, var any val1, var any val2) {
    var string x;
    x = message;
    x = replaceFirst(x, val1);
    x = replaceFirst(x, val2);
    return x;
}

/**
 * String format with one argument, function overloading
 **/
function string stringf1(var string message, var any val1) {
    return stringf2(message, val1, "");
}

// Create a class
class Team {

    // Create variables, members of a class
    var string name;
    var Person p1;
    var Person p2;

    function Person getP1(var Team this) {
        return this.p1;
    }

    function Person getP2(var Team this) {
        return this.p2;
    }

    /**
     * Print details about the team
     */
    function void printDetails(var Team this) {
        print(stringf2("Team {} has {} members", this.name, 2));
        print(stringf2("First is: {}. Second is: {}",this.p1.name, this.p2.name));
    }
}

// Create another class
class Person {
    var string name;
    var int id;
}

// Main function, return an int
function int main(var string name) {

    // If name was not specified
    //if(name == "") {
    //    print("Pleas enter a name as an argument when running this script");
    //    return FAILURE_CODE;
    //}

    // Initialize global variables
    init();

    // Welcome message
    print(stringf1("Hello \"{}\", welcome to BashClass", name));

    // Create 10 teams
    var any test;
    test = "test";
    var int i;
    i=0;
    while(i < 10) {
        var Team team;
        team.name = "Bash team";
        team.p1.name = "One";
        team.getP1(team).id = 1;
        team.p2.name = "Two";
        team.getP2(team).id = 2;
        
        team.printDetails(team);

        i=i+1;
    }
    
    // Run a block of bash
    >@
        echo Thank you for using BashClass
        echo Good bye!
    @<

    // Return 0
    return SUCCESS_CODE;
}
